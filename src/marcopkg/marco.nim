{.experimental: "codeReordering".}
import options
import xmltree
import xmlparser
import strtabs
import sets
import tables

import blob

# References:
# - https://github.com/aosp-mirror/platform_frameworks_base/blob/e5cf74326dc37e87c24016640b535a269499e1ec/tools/aapt/XMLNode.cpp#L1089
# - https://android.googlesource.com/platform/frameworks/base/+/dc36bb6dea837608c29c177a7ea8cf46b6a0cd53/tools/aapt/XMLNode.cpp
# - https://github.com/sdklite/aapt/blob/9e6d1ad98469dffbc9940821551bd7a2e07dd1e0/src/main/java/com/sdklite/aapt/AssetEditor.java

type
  DataType = enum
    dtString = 0x03,
    dtInt = 0x10
  KnownAttr = object
    name: string
    rawDefault: string  # "" means none
    stripRaw: bool
    typ: DataType
  StringSets = object
    res: OrderedSet[string]
    other: OrderedSet[string]
  ManifestError* = object of CatchableError

  ChunkType = enum
    ctStringPool = 0x0001'u16
    ctXML = 0x0003'u16
    ctXMLResourceMap = 0x0180'u16

const
  nsAndroid = "http://schemas.android.com/apk/res/android"
  knownManifestAttrs = @[
    KnownAttr(name: "android:compileSdkVersion", rawDefault: "28", typ: dtInt, stripRaw: true),
    KnownAttr(name: "android:compileSdkVersionCodename", rawDefault: "9", typ: dtString),
    KnownAttr(name: "platformBuildVersionCode", rawDefault: "28", typ: dtInt),
    KnownAttr(name: "platformBuildVersionName", rawDefault: "9", typ: dtInt),
  ]
  knownResources = [
    ("compileSdkVersion", 0x01010572'u32),
    ("compileSdkVersionCodename", 0x01010573'u32),
    ("label", 0x01010001'u32),
    ("name", 0x01010003'u32),
  ].toTable


proc marcoCompile*(inputXml: string): string =
  # Parse + verify namespaces in a dumb, hacky way
  let
    xml = parseXml(inputXml)
  if xml.tag != "manifest":
    raise newException(ManifestError, "root node must be <manifest>")
  if not xml.attrs.hasKey("xmlns:android"):
    raise newException(ManifestError, "the <manifest> node must have 'xmlns:android' attribute")
  if xml.attrs["xmlns:android"] != nsAndroid:
    raise newException(ManifestError, "the <manifest> node's attribute 'xmlns:android' must have value \"" & nsAndroid & "\"")

  # Set default attributes if necessary
  for attr in knownManifestAttrs:
    if attr.name notin xml.attrs:
      xml.attrs[attr.name] = attr.rawDefault

  # Collect all strings
  var buckets = StringSets()
  init(buckets.res)
  init(buckets.other)
  collectStrings(xml, buckets)
  var strings = initOrderedSet[string]()
  for s in buckets.res:
    strings.incl(s)
  for s in buckets.other:
    strings.incl(s)
  # echo buckets.res #.seq[:string]
  # echo buckets.other #.seq[:string]

  # Partially render header
  var res: Blob
  res.put16(ctXML.ord)
  res.put16(8)  # header size
  var fileSizeSlot = res.slot32()

  # Render list of strings
  let stringsPos = res.pos
  res.put16(ctStringPool.ord)
  res.put16(0x1c)  # header size
  var stringsSizeSlot = res.slot32()
  res.put32(uint32(strings.len))
  res.put32(0)  # style count
  res.put32(0)  # flags TODO(akavel): try writing utf-8, not utf-16
  var stringsStartSlot = res.slot32()
  res.put32(0)  # styles start
  var stringOffsetSlots = newSeq[Slot32]()
  for i in 0 ..< strings.len:
    stringOffsetSlots.add(res.slot32())
  let stringsStartPos = res.pos
  res.set(stringsStartSlot, stringsStartPos - stringsPos)
  for i, s in strings:
    res.set(stringOffsetSlots[i], res.pos - stringsStartPos)
    res.put16(s.len.uint16)  # TODO: handle longer strings
    for c in s:
      res.put16(c.ord.uint16)
    res.put16(0)
  res.set(stringsSizeSlot, res.pos - stringsPos)

  # Render "XML resource map"
  let resMapPos = res.pos
  res.put16(ctXMLResourceMap.ord)
  res.put16(8)  # header size
  let resMapSizeSlot = res.slot32()
  for s in buckets.res:
    res.put32(knownResources[s])
  res.set(resMapSizeSlot, res.pos - resMapPos)

  res.set(fileSizeSlot, res.pos)
  result = res.string

proc collectStrings(xml: XmlNode, strings: var StringSets) =
  if xml.kind != xnElement:
    return
  # NOTE(akavel): trying to reproduce the sample file we got
  # generated by aapt, we're adding strings in the following
  # order:
  #  1. namespace keys & values
  #  2. attribute keys
  #  3. tag name
  #  4. attribute values
  # TODO(akavel): in future, try simplifying this to a
  # single OrderedSet or SortedSet
  var
    strNs = newSeq[string]()
    strKeys = newSeq[string]()
    strVals = newSeq[string]()
  if xml.attrsLen > 0:
    for k, v in xml.attrs:
      if k == "xmlns":
        continue
      let (ns, attr) = k.splitQName
      if ns == "xmlns":
        strNs.add(attr)
        strNs.add(v)
        continue
      strKeys.add(attr)
      if k == "android:compileSdkVersion":
        # HACK(akavel): remove this once we verify order is not important
        continue
      strVals.add(v)
  let (ns, tag) = xml.tag.splitQName
  for s in strNs & @[""] & strKeys & @[tag] & strVals:
    strings.incl(s)
  for child in xml:
    collectStrings(child, strings)

proc splitQName(qname: string): (string, string) =
  let split = qname.split(":")
  if split.len > 1:
    return (split[0], split[1])
  else:
    return ("", split[0])

proc incl(strings: var StringSets, item: string) =
  if item in knownResources:
    strings.res.incl(item)
  else:
    strings.other.incl(item)

